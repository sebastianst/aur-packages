# Maintainer:  Sebastian Stammler <stammler.s@gmail.com>
#
# Now pacman 4.1 ready!
# For improvements/fixes to this package, please send me a
# pull request on github: https://github.com/sebastianst/aur-packages
#
# Contributor: Sarkasper <echo a2FzcGVyLm1lbnRlbkBnbXguY29tCg== | base64 -d>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Michalis Georgiou <mechmg93@gmail.comr>
# Contributor: Alexander De Sousa <archaur.xandy21@spamgourmet.com>

pkgname=ttf-google-fonts-git
pkgver=20141001
pkgrel=1
pkgdesc="TrueType fonts from the Google Fonts project."
arch=('any')
url="https://github.com/w0ng/googlefontdirectory"
license=('various')
# About why "cantarell-fonts" is a dependency see comment in package() function.
depends=('cantarell-fonts'
         'fontconfig'
         'xorg-fonts-encodings'
         'xorg-mkfontdir'
         'xorg-mkfontscale')
makedepends=('git')
conflicts=('adobe-source-code-pro-fonts'
           'adobe-source-sans-pro-fonts'
           'googlefontdirectory'
           'jsmath-fonts'
           'lohit-fonts'
           'oldstand-font'
           'openarch_fonts'
           'otf-bitter'
           'otf-goudy'
           'ttf-andika'
           'ttf-anonymous-pro'
           'ttf-cantarell'
           'ttf-cardo'
           'ttf-chromeos-fonts'
           'ttf-comfortaa'
           'ttf-droid'
           'ttf-google-fonts-hg'
           'ttf-google-webfonts'
           'ttf-google-webfonts-git'
           'ttf-google-webfonts-hg'
           'ttf-inconsolata'
           'ttf-kimberly_geswein_print'
           'ttf-lato'
           'ttf-lekton'
           'ttf-medievalsharp'
           'ttf-nova'
           'ttf-oldstandard'
           'ttf-opensans'
           'ttf-oxygen'
           'ttf-oxygen-git'
           'ttf-ptsans'
           'ttf-pt-mono'
           'ttf-pt-sans'
           'ttf-roboto'
           'ttf-sil-fonts'
           'ttf-sortsmillgoudy'
           'ttf-source-code-pro'
           'ttf-source-sans-pro'
           'ttf-ubuntu-font-family'
           'ttf-vollkorn')
provides=("${conflicts[@]}"
          'ttf-font')
install=font.install
source=("git://github.com/w0ng/googlefontdirectory.git")
md5sums=('SKIP')

pkgver() {
  _gitname="googlefontdirectory"

  cd "$srcdir/$_gitname"
  git log -1 --format="%cd" --date=short | sed 's|-||g'
}

package() {

  # Font groups can be defined if wanting to be selective (OpenSans Tinos...),
  # otherwise leave blank for all.
  font_incl=()

  cd "$srcdir"
  _pkgdir=$pkgdir/usr/share/fonts/TTF
  install -d $_pkgdir

  if ! (( "${#font_incl[@]}" )); then
    find . -type f -name *.ttf -exec install -Dm644 '{}' $_pkgdir \;
  else
    for font in "${font_incl[@]}"; do
      find . -type f -name "$font"*.ttf -exec install -Dm644 '{}' $_pkgdir \;
    done
  fi

  # Remove Cantarell fonts because Google ships the original Cantarell
  # instead of the improved version of Cantarell shipped by the GNOME Project.
  find $_pkgdir -type f -name "Cantarell-*.ttf" -delete

}

# vim:set ts=2 sw=2 et:
